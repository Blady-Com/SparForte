1  HIMEM: 36860
2  TEXT : HOME : CALL  - 1184: POKE 37,0: POKE 36,23: PRINT "+": HTAB 14: VTAB (2): PRINT "STAR TREK ]I[": PRINT : PRINT 
3  DIM Z$(8,8),G1$(17),R$(11)
4  FOR I = 1 TO 8: FOR J = 1 TO 8:Z$(I,J) = "   ": NEXT J,I
5  DIM G(8,8),C(9,2),K(3,3),N(3),Z(8,8)
6 T =  INT ( RND (1) * 20 + 20) * 100
7 T0 = T:T9 = 30:D0 = 0:E0 = 3000:E = E0:P = 10:P0 = P:S9 = 200:S = 0
8  DEF  FN D(D) =  SQR ((K(I,1) - S1) ^ 2 + (K(I,2) - S2) ^ 2)
9 Q1 =  INT ( RND (1) * 8 + 1):Q2 =  INT ( RND (1) * 8 + 1):S1 =  INT ( RND (1) * 8 + 1):S2 =  INT ( RND (1) * 8 + 1)
10  FOR I = 0 TO 9: FOR J = 0 TO 2:C(I,J) = 0: NEXT J,I
11 C(2,1) =  - 1:C(3,1) =  - 1:C(4,1) =  - 1:C(4,2) =  - 1:C(5,2) =  - 1:C(6,2) =  - 1:C(1,2) = 1:C(2,2) = 1:C(6,1) = 1:C(7,1) = 1:C(8,1) = 1:C(8,2) = 1:C(9,2) = 1
12  DIM D(8),D$(8)
13  FOR I = 1 TO 8:D(I) = 0: NEXT 
14 FA$ = "FATAL":ER$ = "ERROR!"
15 A1$ = "NSLPTSDCX"
16 G4$ = "III"
17  DATA ANTARES, SIRIUS, RIGEL, DENEB, PROCYON, CAPELLA, VEGA, BETELGEUSE, CANOPUS, ALDEBARAN, ALTAIR, REGULUS, SAGITTARIUS, ARCTURUS, POLLUX, SPICA 
18  DATA WARP ENGINES,SR SENSORS,LR SENSORS,PHASER CNTRL,PH TUBES,DAMAGE CNTRL,SHIELD CNTRL,COMPUTER
19  FOR X = 1 TO 16: READ G1$(X): NEXT 
20  FOR X = 1 TO 8: READ D$(X): NEXT 
21  GOSUB 607
22 B9 = 0:K9 = 0:A1$ = "NSLPTSDCX"
23  FOR I = 1 TO 8: FOR J = 1 TO 8:R1 =  RND ( -  RND (1) * 1E9):R1 =  RND (1)
24  IF R1 > .95 THEN 29
25  IF R1 > .9 THEN 30
26  IF R1 > .8 THEN 31
27 K3 = 0
28  GOTO 32
29 K3 = 3:K9 = K9 + 3: GOTO 32
30 K3 = 2:K9 = K9 + 2: GOTO 32
31 K3 = 1:K9 = K9 + 1
32 R1 =  RND (1): IF R1 > .95 THEN 34
33 B3 = 0: GOTO 35
34 B3 = 1:B9 = B9 + 1
35 S3 =  INT ( RND (1) * 7 + 1)
36 G(I,J) = K3 * 100 + B3 * 10 + S3
37 Z(I,J) = 0
38  NEXT J
39  NEXT I
40 K7 = K9
41 X$ = "":X0$ = " IS ": IF B9 <  > 0 THEN 46
42 B9 = 1: IF G(6,3) >  = 200 THEN 45
43 G(6,3) = G(6,3) + 100
44 K9 = K9 + 1
45 G(6,3) = G(6,3) + 10
46  IF B9 = 1 THEN 48
47 X$ = "S":X0$ = " ARE "
48  PRINT "YOUR ORDERS ARE AS FOLLOWS :"
49  PRINT "DESTROY THE "K9" KLINGON WARSHIPS WHICH   HAVE INVADED";
50  PRINT " THE GALAXY BEFORE THEY CAN ATTACK FEDERATION HEADQUARTERS";
51  PRINT " ON       STARDATE "T0 + T9"; THIS GIVES YOU "T9" DAYS.": PRINT "THERE"X0$;
52  PRINT B9" STARBASE"X$" IN THE GALAXY FOR": PRINT "RESUPPLYING YOUR SHIP.": PRINT 
53  INPUT "HIT RETURN TO ASSUME COMMAND - - -";A$: HOME 
54  VTAB 16: PRINT " ";: INVERSE : PRINT "1 2 3 4 5 6 7 8 ";: NORMAL : FOR I = 1 TO 8: HTAB 1: VTAB 16 + I: PRINT I;: NEXT : VTAB 1
55  GOSUB 565
56 Z4 = Q1:Z5 = Q2:K3 = 0:B3 = 0:S3 = 0:G5 = 0:D4 = .5 *  RND (1)
57  IF Q1 < 1 OR Q1 > 8 OR Q2 < 1 OR Q2 > 8 THEN 160
58  GOSUB 527
59  POKE 33,40: POKE 32,0: POKE 35,15: POKE 34,0: HOME : POKE 34,1: VTAB 1: INVERSE : PRINT "QUADRANT";: NORMAL : PRINT "  ";: INVERSE : PRINT G2$: NORMAL 
60  IF T <  > T0 THEN 64
61  PRINT "YOUR MISSION BEGINS IN :"
62  PRINT "GALACTIC QUADRANT, '"G2$"'"
63  GOTO 65
64  PRINT "NOW ENTERING "G2$" QUADRANT.."
65  PRINT 
66 X = G(Q1,Q2)
67 K3 =  INT (X / 100)
68 B3 =  INT (X / 10 - K3 * 10)
69 S3 = X - B3 * 10 - K3 * 100:S3 =  INT (S3)
70  IF K3 = 0 THEN 74
71  PRINT "COMBAT AREA    CONDITION ";: FLASH : PRINT "RED": NORMAL 
72  IF S > 200 THEN 74
73  INVERSE : PRINT "SHIELDS DANGEROUSLY LOW": NORMAL 
74  FOR I = 1 TO 3: FOR J = 1 TO 3:K(I,J) = 0: NEXT J,I
75  FOR I = 1 TO 8: FOR J = 1 TO 8:Z$(I,J) = " ": NEXT J,I
76 A$ = "E":Z1 = S1:Z2 = S2: GOSUB 520
77  IF K3 = 0 THEN 82
78  FOR I = 1 TO K3: GOSUB 518:A$ = "K"
79 Z1 = R1:Z2 = R2: GOSUB 520
80 K(I,1) = R1:K(I,2) = R2:K(I,3) = S9
81  NEXT 
82  IF B3 = 0 THEN 86
83  FOR I = 1 TO B3: GOSUB 518:A$ = "B"
84 Z1 = R1:Z2 = R2: GOSUB 520:B4 = Z1:B5 = Z2
85  NEXT 
86  IF S3 = 0 THEN 90
87  FOR I = 1 TO S3: GOSUB 518:A$ = "*"
88 Z1 = R1:Z2 = R2: GOSUB 520
89  NEXT 
90  GOSUB 421
91  GOSUB 574: IF D(2) < 0 THEN  POKE 34,16: POKE 33,17: POKE 35,24: POKE 32,1: HOME : POKE 33,40: POKE 32,0
92  POKE 34,14: POKE 35,15: HOME : POKE 34,1
93  IF D0 = 1 THEN 102
94  IF S + E <  = 10 THEN 97
95  IF E > 10 THEN 102
96  IF D(7) >  = 0 THEN 102
97  TEXT : HOME : FLASH : FOR I = 1 TO 5: CALL 37024: PRINT  MID$ (FA$,I,1);: NEXT : NORMAL : PRINT " ";: FLASH : FOR I = 1 TO 6: CALL 37024: PRINT  MID$ (ER$,I,1);: NEXT : NORMAL : PRINT 
98  PRINT "YOU ARE STRANDED IN SPACE !": PRINT 
99 : PRINT "YOU HAVE INSUFFICIENT ENERGY FOR": PRINT "MANOEUVRING";: IF D(7) >  = 0 THEN  PRINT : GOTO 411
100  PRINT " AND SHIELD CONTROL IS": PRINT "CURRENTLY INCAPABLE OF CROSSCIRCUITING": PRINT "TO ENGINE ROOM": PRINT 
101  GOTO 411
102  CALL 37024: INPUT "COMMAND ?";A$
103 Z(Q1,Q2) = G(Q1,Q2)
104  FOR I = 1 TO 9
105  IF  LEFT$ (A$,1) <  >  MID$ (A1$,I,1) THEN 110
106  IF I <  > 2 OR  LEN (A$) < 2 OR  MID$ (A$,2,1) = "R" THEN 108
107 I = 6
108  ON I GOTO 124,90,224,244,286,344,353,440
109  IF A$ = "XXX" THEN 413
110  NEXT 
111  HOME 
112  PRINT "ENTER ONE OF THE FOLLOWING:"
113  PRINT "NAV (TO SET COURSE)"
114  PRINT "SRS (FOR SHORT RANGE SENSOR SCAN)"
115  PRINT "LRS (FOR LONG RANGE SENSOR SCAN)"
116  PRINT "PHA (TO FIRE PHASERS)"
117  PRINT "TOR (TO FIRE PHOTON TORPEDOES)"
118  PRINT "SHE (TO SET SHIELD ENERGY)"
119  PRINT "DAM (FOR DAMAGE CONTROL REPORT)"
120  PRINT "COM (TO CALL ON LIBRARY- COMPUTER)"
121  PRINT "XXX (TO RESIGN YOUR COMMAND)"
122  PRINT 
123  GOTO 91
124  INPUT "COURSE (1-9) ?";C1
125  IF C1 >  = 1 THEN 127
126  PRINT "INCORRECT COURSE DATA ": GOTO 91
127  IF C1 < 9 THEN 130
128  IF C1 > 9 THEN 126
129 C1 = 1
130  INPUT "WARP FACTOR (0-8) ?";W1
131  IF W1 <  = 0 THEN 133
132  IF W1 <  = 8 THEN 135
133  PRINT "THE ENGINES WON'T TAKE WARP "W1" !"
134  GOTO 91
135  IF D(1) >  = 0 OR W1 <  = .2 THEN 138
136  PRINT "ENGINES DAMAGED. MAX SPEED = WARP 0.2"
137  GOTO 124
138 N =  INT (W1 * 8 + .5)
139  IF E - N >  = 0 THEN 144
140  PRINT "INSUFFICIENT ENERGY FOR WARP "W1
141  IF S < N - E OR D(7) < 0 THEN 91
142  PRINT S" UNITS OF ENERGY DEPLOYED TO SHIELDS"
143  GOTO 344
144  FOR I = 1 TO K3
145  IF K(I,3) <  = 0 THEN 149
146 A$ = " ":Z1 = K(I,1):Z2 = K(I,2):X3 = Z1:Y3 = Z2: GOSUB 520: INVERSE : GOSUB 586: NORMAL : GOSUB 518
147  FOR W = 0 TO 300: NEXT 
148 K(I,1) = Z1:K(I,2) = Z2:A$ = "K":X3 = Z1:Y3 = Z2: INVERSE : GOSUB 586: NORMAL : GOSUB 520
149  NEXT I
150  GOSUB 384:D1 = 0:D6 = W1
151  IF FL = 1 THEN FL = 0: GOTO 91
152  IF W1 < 1 THEN 154
153 D6 = 1
154  FOR I = 1 TO 8
155  IF D(I) >  = 0 THEN 161
156 D(I) = D(I) + D6
157  IF D(I) < 0 THEN 161
158  IF D1 = 1 THEN 160
159 D1 = 1: PRINT "DAMAGE REPORT: ";
160  HTAB 15:R1 = I: GOSUB 523: PRINT " REPAIRED"
161  NEXT I
162  IF  RND (1) > .2 THEN 171
163 R1 =  INT ( RND (1) * 8 + 1)
164  IF  RND (1) >  = .6 THEN 169
165 D(R1) = D(R1) - ( RND (1) * 5 + 1)
166  PRINT 
167  PRINT "DAMAGE REPORT: ";: FLASH : GOSUB 523: PRINT " DAMAGED": NORMAL 
168  GOTO 171
169 D(R1) = D(R1) +  RND (1) * 3 + 1: PRINT 
170  PRINT "DAMAGE REPORT: ";: GOSUB 523: PRINT " IMPROVED"
171 A$ = " ":Z1 =  INT (S1 + .5):Z2 =  INT (S2 + .5):X3 = Z1:Y3 = Z2: GOSUB 586: INVERSE : GOSUB 586: NORMAL : HTAB 1: VTAB 15
172  GOSUB 520
173 X1 = C(C1,1) + (C(C1 + 1,1) - C(C1,1)) * (C1 -  INT (C1))
174 X = S1:Y = S2
175 X2 = C(C1,2) + (C(C1 + 1,2) - C(C1,2)) * (C1 -  INT (C1))
176 Q4 = Q1:Q5 = Q2
177  FOR I = 1 TO N
178 S1 = S1 + X1:S2 = S2 + X2
179  IF  INT (S1 + .5) < 1 OR  INT (S1 + .5) > 8 OR  INT (S2 + .5) < 1 OR  INT (S2 + .5) > 8 THEN 196
180  IF Z$( INT (S1 + .5), INT (S2 + .5)) = " " THEN 184
181  IF Z$( INT (S1 + .5), INT (S2 + .5)) = "B" THEN D0 = 1
182 S1 = S1 - X1:S2 = S2 - X2
183  PRINT "ENGINES SHUT DOWN AT SECTOR " INT (S1 + .5)",";: INVERSE : PRINT  INT (S2 + .5): PRINT "DUE TO BAD NAVIGATION ": NORMAL : GOTO 185
184  NEXT I
185 A$ = "E":Z1 =  INT (S1 + .5):Z2 =  INT (S2 + .5):X3 = Z1:Y3 = Z2: GOSUB 586: INVERSE : GOSUB 586: NORMAL : HTAB 1: VTAB 15
186  GOSUB 520: GOSUB 217
187 T8 = 1: IF W1 >  = 1 THEN 189
188 T8 = .1 *  INT (10 * W1)
189 T = T + T8
190  IF T > T0 + T9 THEN 405
191  FOR I = S1 - 1 TO S1 + 1: FOR J = S2 - 1 TO S2 + 1: IF I < 1 OR I > 8 OR J < 1 OR J > 8 THEN 193
192 A$ = "B":Z1 = I:Z2 = J: GOSUB 524: IF Z3 = 1 THEN D0 = 1
193  NEXT J,I
194  IF D0 = 1 THEN 90
195  GOTO 91
196 X = 8 * Q1 + X + N * X1:Y = 8 * Q2 + Y + N * X2:Q1 =  INT (X / 8):Q2 =  INT (Y / 8):S1 =  INT (X - Q1 * 8):S2 =  INT (Y - Q2 * 8)
197  IF S1 <  > 0 THEN 199
198 Q1 = Q1 - 1:S1 = 8
199  IF S2 <  > 0 THEN 201
200 Q2 = Q2 - 1:S2 = 8
201 X5 = 0: IF Q1 >  = 1 THEN 203
202 X5 = 1:Q1 = 1:S1 = 1
203  IF Q1 <  = 8 THEN 205
204 X5 = 1:Q1 = 8:S1 = 8
205  IF Q2 >  = 1 THEN 207
206 X5 = 1:Q2 = 1:S2 = 1
207  IF Q2 <  = 8 THEN 209
208 X5 = 1:Q2 = 8:S2 = 8
209  IF X5 = 0 THEN 213
210  FLASH : PRINT "ATTEMPT TO CROSS GALACTIC PERIMETER:": NORMAL 
211  PRINT "ENGINES SHUT DOWN AT SECTOR " INT (S1 + .5)",";: INVERSE : PRINT  INT (S2 + .5): NORMAL : PRINT "                OF QUADRANT " INT (Q1 + .5)",";: INVERSE : PRINT  INT (Q2 + .5): NORMAL : FOR I = 0 TO 10000: NEXT 
212  IF T > T0 + T9 THEN 405
213  IF 8 * Q1 + Q2 = 8 * Q4 + Q5 THEN 185
214 T = T + 1
215  GOSUB 217
216  GOTO 56
217 E = E - N - 10
218  IF E >  = 0 THEN 223
219  PRINT "SHIELD CONTROL SUPPLIED ENERGY TO": PRINT " TO COMPLETE THE MANOEUVER."
220 S = S + E:E = 0
221  IF S > 0 THEN 223
222 S = 0
223  RETURN 
224  IF D(3) >  = 0 THEN 226
225  PRINT "LR SENSORS INOPERABLE": GOTO 91
226  PRINT "LR SENSOR SCAN FOR QUADRANT "Q1",";: INVERSE : PRINT Q2: NORMAL 
227 O1$ = "-------------"
228  PRINT O1$
229  FOR I = Q1 - 1 TO Q1 + 1: PRINT ":";
230  FOR J = Q2 - 1 TO Q2 + 1
231  IF J > 8 OR I > 8 OR J < 1 OR I < 1 THEN  PRINT "   :";: GOTO 239
232 Z(I,J) = G(I,J):Z1 = G(I,J)
233  IF Z1 >  = 100 THEN Z$ =  STR$ (Z1): GOTO 236
234  IF Z1 >  = 10 THEN Z$ = " " +  STR$ (Z1): GOTO 236
235 Z$ = "  " +  STR$ (Z1)
236  IF I = Q1 AND J = Q2 THEN  INVERSE : PRINT Z$;: NORMAL : PRINT ":";: GOTO 239
237  IF  VAL ( MID$ (Z$,2,1)) > 0 THEN  PRINT  LEFT$ (Z$,1);: INVERSE : PRINT  MID$ (Z$,2,1);: NORMAL : PRINT  RIGHT$ (Z$,1)":";: GOTO 239
238  PRINT Z$":";
239  NEXT 
240  PRINT 
241  NEXT 
242  PRINT O1$
243  GOTO 91
244  IF K3 > 0 THEN 246
245  PRINT "NO KLINGON SHIPS IN THIS QUADRANT": GOTO 91
246  IF D(4) >  = 0 THEN 248
247  PRINT "PHASERS INOPERATIVE": GOTO 91
248  IF D(8) >  = 0 THEN 250
249  PRINT "COMPUTER FAILURE HAMPERS ACCURACY"
250  PRINT "PHASERS LOCKED ON TARGET"
251  PRINT "ENERGY AVAILABLE = "E
252  INPUT "UNITS TO FIRE ?";X
253  IF X <  = 0 THEN 91
254  IF E - X < 0 THEN 251
255 E = E - X
256  GOSUB 384
257  IF D(7) >  = 0 THEN 259
258 X = X *  RND (1)
259 H1 =  INT (X / K3)
260  FOR I = 1 TO 3
261  IF K(I,3) <  = 0 THEN 284
262  CALL 37296
263 H =  INT ((H1 /  FN D(0)) * ( RND (1) + 2))
264  IF H > .15 * K(I,3) THEN 268
265  PRINT "SENSORS SHOW NO DAMAGE"
266  PRINT "    TO ENEMY AT SECTOR "K(I,1)",";: INVERSE : PRINT K(I,2): NORMAL 
267  GOTO 284
268 K(I,3) = K(I,3) - H
269  IF D(2) < 0 THEN 273
270  HTAB K(I,2) * 2: VTAB K(I,1) + 16: PRINT "K";: FOR W = 0 TO 200: NEXT 
271  INVERSE : HTAB K(I,2) * 2: VTAB K(I,1) + 16: PRINT "K";: NORMAL 
272  IF D(2) < 0 THEN 273
273  HTAB 1: VTAB 15
274  PRINT H" UNIT HIT ON KLINGON AT "K(I,1)",";: INVERSE : PRINT K(I,2): NORMAL 
275  IF K(I,3) <  = 0 THEN X3 = K(I,1):Y3 = K(I,2):A$ = "K": GOSUB 588: GOTO 278
276  PRINT "SENSORS SHOW "K(I,3)" UNITS LEFT"
277  GOTO 284
278  FLASH : PRINT "*KLINGON DESTROYED*": NORMAL 
279 K3 = K3 - 1:K9 = K9 - 1:A$ = " ":Z1 = K(I,1):Z2 = K(I,2): GOSUB 520
280  IF K3 = 0 THEN C$ = "GREEN"
281  IF K3 = 0 AND E < E0 * .1 THEN C$ = "YELLOW"
282 G(Q1,Q2) = K3 * 100 + B3 * 10 + S3
283  IF K9 <  = 0 THEN 418
284  NEXT I
285  GOTO 91
286  IF D(5) >  = 0 THEN 288
287  PRINT "PH TUBES INOPERABLE": GOTO 91
288  IF P > 0 THEN 290
289  PRINT "ALL PH TORPS EXPENDED": GOTO 91
290  INPUT "TORPEDO COURSE ?";C1
291  IF C1 >  = 1 THEN 293
292  PRINT "INCORRECT COURSE DATA !": GOTO 91
293  IF C1 > 9 THEN 292
294  IF C1 < 9 THEN 297
295  IF C1 >  = 9 THEN 290
296 C1 = 1
297 X1 = C(C1,1) + (C(C1 + 1,1) - C(C1,1)) * (C1 -  INT (C1))
298 X2 = C(C1,2) + (C(C1 + 1,2) - C(C1,2)) * (C1 -  INT (C1))
299 E = E - 2:X = S1:Y = S2:P = P - 1
300  PRINT "TRACKING TORPEDO"
301  CALL 36864
302 X = X + X1:Y = Y + X2:X3 =  INT (X + .5):Y3 =  INT (Y + .5)
303  IF X3 < 1 OR X3 >  = 9 OR Y3 < 1 OR Y3 >  = 9 THEN 341
304  IF D(2) < 0 THEN 306
305  VTAB X3 + 16: HTAB Y3 * 2: PRINT "#";: FOR X4 = 0 TO 20: NEXT : INVERSE : HTAB Y3 * 2: VTAB X3 + 16: PRINT " ";: NORMAL 
306  VTAB 15: HTAB 1
307 A$ = " ":Z1 = X:Z2 = Y: GOSUB 524
308  IF Z3 <  > 0 THEN 302
309 A$ = "K":Z1 = X:Z2 = Y: GOSUB 524
310  IF Z3 = 0 THEN 322
311  GOSUB 588
312  INVERSE : PRINT "*KLINGON DESTROYED*": NORMAL 
313 K3 = K3 - 1:K9 = K9 - 1: IF K9 <  = 0 THEN 418
314  IF K3 = 0 THEN C$ = "GREEN"
315  IF K3 = 0 AND E < E0 * .1 THEN C$ = "YELLOW"
316  FOR I = 1 TO 3
317  IF X3 <  > K(I,1) THEN 319
318  IF Y3 = K(I,2) THEN 320
319  NEXT I: GOTO 321
320 K(I,3) = 0
321  GOTO 338
322 A$ = "*":Z1 = X:Z2 = Y: GOSUB 524
323  IF Z3 = 0 THEN 329
324  GOSUB 588
325  PRINT "STAR AT "X3",";: INVERSE : PRINT Y3;: NORMAL : PRINT " ABSORBED ENERGY"
326  IF D(2) < 0 THEN 342
327  HTAB Z2 * 2: VTAB Z1 + 16: PRINT "*";: FOR X4 = 0 TO 500: NEXT : INVERSE : HTAB Z2 * 2: VTAB Z1 + 16: PRINT "*";: HTAB 1: VTAB 15: NORMAL 
328  GOTO 342
329 A$ = "B":Z1 = X:Z2 = Y: GOSUB 524
330  IF Z3 = 0 THEN 290
331  GOSUB 588
332  INVERSE : PRINT "STARBASE DESTROYED!": NORMAL 
333 B3 = B3 - 1:B9 = B9 - 1: IF B9 > 0 THEN 336
334  PRINT : PRINT "THAT DOES IT, CAPTAIN !": PRINT "YOU ARE HEREBY RELIEVED OF COMMAND"
335  PRINT : GOTO 413
336  PRINT "COURTMARTIAL UNDER CONSIDERATION!"
337 D0 = 0
338 A$ = " ":Z1 = X:Z2 = Y: GOSUB 520
339 G(Q1,Q2) = K3 * 100 + B3 * 10 + S3
340  GOTO 342
341  PRINT "TORPEDO MISSED"
342 FL = 1: GOTO 144
343  GOTO 91
344  IF D(7) >  = 0 THEN 346
345  INVERSE : PRINT "SHIELD CONTROL INOPERABLE": NORMAL : GOTO 91
346  PRINT "ENERGY = "E + S;: INPUT "  UNITS TO SHIELDS ";X
347  IF X >  = 0 OR S <  > X THEN 349
348  PRINT "SHIELDS UNCHANGED": GOTO 91
349  IF E + S - X < 0 THEN 346
350 E = E + S - X:S = X
351  PRINT "SHIELDS NOW AT "S" UNITS"
352  GOTO 91
353  IF D(6) >  = 0 THEN 371
354  PRINT "DAMAGE REPORT NOT AVAILABLE"
355  IF D0 = 0 THEN 91
356 D3 = 0
357  FOR I = 1 TO 8
358  IF D(I) >  = 0 THEN 360
359 D3 = D3 + .1
360  NEXT I
361  IF D3 = 0 THEN 91
362 D3 = D3 + D4:D3 =  INT (D3 * 100) / 100
363  IF D3 < 1 THEN 365
364 D3 = .9
365  PRINT "ESTIMATED REPAIR TIME = "D3" STARDATES"
366  INPUT "DO YOU AUTHORIZE REPAIR ORDER ?";A$
367  IF  LEFT$ (A$,1) <  > "Y" THEN 91
368  FOR I = 1 TO 8:D(I) = 0: NEXT 
369 T = T + D3 + .1
370  GOSUB 558
371  REM 
372  PRINT "DEVICE        STATE OF REPAIR"
373  FOR R1 = 1 TO 8: GOSUB 523
374 DA =  INT (D(R1) * 100) / 100:DB =  ABS (DA)
375  IF DB = 0 THEN D$ = "0.00"
376  IF DB > 0 AND DB < 1 THEN D$ = "0" +  STR$ (DB)
377  IF DB >  = 1 AND DB < 10 THEN D$ =  STR$ (DB)
378  IF  LEN (D$) = 3 THEN D$ = D$ + "0"
379  IF  LEN (D$) = 1 THEN D$ = D$ + ".00"
380  HTAB 19: IF DA < 0 THEN  HTAB 18: INVERSE : PRINT "-";
381  IF DB >  = 10 THEN D$ =  STR$ (DB): IF  LEN (D$) = 4 THEN D$ = D$ + "0"
382  PRINT D$: NORMAL : NEXT R1
383  GOTO 355
384  IF K3 <  = 0 THEN 404
385  IF D0 = 0 THEN 388
386  PRINT "STAR BASE SHIELDS PROTECT ENTERPRISE"
387  GOTO 404
388  FOR I = 1 TO 3
389  IF K(I,3) <  = 0 THEN 403
390  IF D(2) < 0 THEN 392
391  HTAB S2 * 2: VTAB S1 + 16: PRINT "E";
392 H =  INT ((K(I,3) /  FN D(0)) * (2 +  RND (1))):S = S - H
393  IF D(2) < 0 THEN 395
394  INVERSE : VTAB S1 + 16: HTAB S2 * 2: PRINT "E";: NORMAL :
395  CALL 37072: VTAB 15: HTAB 1
396  INVERSE : PRINT H" UNIT HIT ";: NORMAL : PRINT "FROM SECTOR "K(I,1)",";: INVERSE : PRINT K(I,2): NORMAL 
397  CALL 36864
398  IF S < 0 THEN 406
399  INVERSE : PRINT "SHIELDS DOWN TO "S" UNITS": NORMAL 
400  IF H < 20 OR  RND (1) > .6 OR H / S <  = .02 THEN 403
401 D2 = H / S + .5 *  RND (1):R1 =  INT ( RND (1) * 8 + 1):D(R1) = D(R1) - D2
402  PRINT "DAMAGE REPORT: ";: FLASH : GOSUB 523: PRINT " DAMAGED!": NORMAL 
403  NEXT I
404  RETURN 
405  TEXT : HOME : PRINT : PRINT "IT IS STARDATE "T: PRINT : GOTO 412
406  POKE 49234,0: POKE 49238,0: POKE 49237,0: POKE 49232,0
407  FOR J = 0 TO 5: CALL 36960: POKE 49236,0: CALL 36960: POKE 49237,0: NEXT 
408  POKE 49233,0: POKE 49236,0: TEXT : HOME : CALL 36912: CALL 37216: FLASH 
409  PRINT "THE ENTERPRISE HAS BEEN DESTROYED": PRINT : INVERSE : PRINT "THE FEDERATION WILL BE CONQUERED": NORMAL 
410  PRINT 
411  PRINT "IT IS STARDATE "T"."
412  REM 
413 W$ = "WAS":K$ = "": IF K9 <  > 1 THEN W$ = "WERE":K$ = "S"
414  PRINT : PRINT "THERE "W$" "K9" KLINGON"K$" LEFT AT THE": PRINT "END OF YOUR MISSION.": PRINT "YOUR EFFINCIENCY RATING IS: ": PRINT (((K7 - K9) / (T - T0)) * 1000): PRINT 
415  PRINT : INPUT "ANOTHER MISSION ?";A$
416  IF  LEFT$ (A$,1) = "Y" THEN  CLEAR : HOME : GOTO 3
417  GOTO 542
418  TEXT : HOME : FLASH : PRINT "CONGRATULATIONS!": NORMAL : CALL 37136: HOME : INVERSE : PRINT "CONGRATULATIONS!": NORMAL : PRINT : PRINT "THE LAST KLINGON HAS BEEN DESTROYED": PRINT 
419  PRINT "YOUR EFFICIENCY RATING IS "((K7 / (T - T0)) * 1000)"."
420  GOTO 415
421  FOR I = S1 - 1 TO S1 + 1: FOR J = S2 - 1 TO S2 + 1
422  IF  INT (I + .5) < 1 OR  INT (I + .5) > 8 OR  INT (J + .5) < 1 OR  INT (J + .5) > 8 THEN 424
423 A$ = "B":Z1 = I:Z2 = J: GOSUB 524: IF Z3 = 1 THEN 426
424  NEXT J,I
425 D0 = 0: GOTO 429
426 D0 = 1:C$ = "DOCKED":E = 3000:P = 10
427  PRINT "SHIELDS DROPPED FOR DOCKING PURPOSES"
428 S = 0: GOTO 434
429  IF K3 > 0 THEN 432
430  IF E < E0 * .1 THEN 433
431 C$ = "GREEN": GOTO 434
432 C$ = "RED": GOTO 434
433 C$ = "YELLOW"
434  IF D(2) >  = 0 THEN 437
435  PRINT 
436  PRINT "*** SHORT RANGE SENSORS ARE OUT ***": PRINT : GOTO 439
437  GOSUB 558: GOTO 439
438  INVERSE : FOR J = 1 TO 8: PRINT Z$(I,J)" ";: NEXT : NORMAL : RETURN 
439  RETURN 
440  IF D(8) >  = 0 THEN 442
441  PRINT "COMPUTER DISABLED": GOTO 91
442  INPUT "COMPUTER AWAITING COMMAND: ";A
443  IF A < 0 THEN 91
444 H8 = 1
445  IF A = 0 THEN 459
446  ON A GOTO 477,486,513,491,517,543,554,561
447  GOTO 448
448  PRINT "COMPUTER FUNCTIONS: "
449  PRINT "   0 = CUMULATIVE GALACTIC RECORD"
450  PRINT "   1 = STATUS REPORT"
451  PRINT "   2 = PHOTON TORPEDO DATA"
452  PRINT "   3 = STARBASE NAVIGATION DATA"
453  PRINT "   4 = GALAXY NAVIGATION CALCULATOR"
454  PRINT "   5 = SECTOR NAVIGATION CALCULATOR"
455  PRINT "   6 = QUADRANT NAME CHART"
456  PRINT "   7 = EFFICIENCY RATING"
457  PRINT "   8 = # DAYS FOR SPECIFIED RATING"
458  GOTO 442
459  HOME : PRINT "GALACTIC RECORD FOR QUADRANT "Q1",";: INVERSE : PRINT Q2
460  PRINT "    1   2   3   4   5   6   7   8  "
461  NORMAL 
462  FOR I = 1 TO 8: PRINT I" :";
463  FOR J = 1 TO 8
464 Z1 = Z(I,J)
465  IF Z1 >  = 100 THEN N$ =  STR$ (Z1): GOTO 469
466  IF Z1 >  = 10 THEN N$ = " " +  STR$ (Z1): GOTO 469
467 N$ = "  " +  STR$ (Z1)
468  IF N$ = "  0" THEN N$ = "   "
469  IF I = Q1 AND J = Q2 THEN  FLASH : PRINT N$;: NORMAL : PRINT " ";: GOTO 473
470  IF  VAL ( MID$ (N$,2,1)) > 0 THEN  PRINT  LEFT$ (N$,1);: INVERSE : PRINT  MID$ (N$,2,1);: NORMAL : PRINT  RIGHT$ (N$,1)" ";: GOTO 473
471  IF N$ = "   " THEN N$ = "   "
472  PRINT N$;: NORMAL : PRINT ":";
473  NEXT J
474  PRINT 
475  NEXT I
476  GOTO 91
477  HOME : PRINT "STATUS REPORT:":X$ = "": IF K9 = 1 THEN 479
478 X$ = "S"
479  PRINT K9" KLINGON"X$" LEFT"
480 V5 = (T0 + T9) - T
481  PRINT "TIME LEFT = " INT (V5 * 100) / 100" STARDATES"
482 X$ = "": IF B9 = 1 THEN 485
483 X$ = "S": IF B9 <  > 0 THEN 485
484  PRINT "YOU ARE ALONE IN THE GALAXY ---": PRINT "YOU HAVE NO STARBASES LEFT!!": GOTO 353
485  PRINT B9" STARBASES REMAIN IN THE GALAXY": GOTO 353
486 H8 = 0
487  FOR I = 1 TO 3
488  IF K(I,3) <  = 0 THEN 511
489 W1 = K(I,1):X = K(I,2)
490 C1 = S1:A = S2: GOTO 494
491  PRINT "QUADRANT NAVIGATION CALCULATOR"
492 C1 = Q1:A = Q2
493  INPUT "DESTINATION COORDINATES (X,Y) ?";W1,X
494  IF C1 = W1 AND A = X THEN 91
495  IF W1 = C1 AND A < X THEN V5 = 1: GOTO 504
496  IF W1 = C1 AND A > X THEN V5 = 5: GOTO 504
497  IF W1 < C1 AND A = X THEN V5 = 3: GOTO 504
498  IF W1 > C1 AND A = X THEN V5 = 7: GOTO 504
499 X7 = (( ATN ( ABS ((W1 - C1) / (X - A)))) / 6.28) * 8
500  IF W1 < C1 AND A < X THEN V5 = X7 + 1
501  IF W1 < C1 AND A > X THEN V5 = 5 - X7
502  IF W1 > C1 AND A < X THEN V5 = 9 - X7
503  IF W1 > C1 AND A > X THEN V5 = 5 + X7
504  PRINT "DIRECTION = " INT (V5 * 100 + .5) / 100" : ";
505 V1 =  ABS (W1 - C1):V2 =  ABS (X - A)
506  IF V1 > V2 OR V1 = V2 THEN X = V1
507  IF V2 > V1 THEN X = V2
508  IF H8 = 1 THEN  PRINT "DISTANCE = ";X: GOTO 510
509  IF H8 < 1 THEN  PRINT "DISTANCE = " INT ( SQR (V1 ^ 2 + V2 ^ 2)) / 10: IF H8 =  - 1 THEN H8 = 1
510  IF H8 = 1 THEN 91
511  NEXT I
512  GOTO 91
513  IF B3 >  < 0 THEN 515
514  PRINT "NO STARBASES IN THIS QUADRANT": GOTO 91
515  PRINT "FROM ENTERPRISE TO STARBASE:"
516 W1 = B4:X = B5:H8 =  - 1: GOTO 490
517 H8 =  - 1:C1 = S1:A = S2: GOTO 493
518 R1 =  RND ( -  RND (1) * 1E9):R1 =  INT ( RND (1) * 8 + 1):R2 =  INT ( RND (1) * 8 + 1):A$ = " ":Z1 = R1:Z2 = R2: GOSUB 524: IF Z3 = 0 THEN 518
519  RETURN 
520 Z1 =  INT (Z1 + .5):Z2 =  INT (Z2 + .5)
521 Z$(Z1,Z2) = A$
522  RETURN 
523  PRINT D$(R1);: RETURN 
524 Z1 =  INT (Z1 + .5):Z2 =  INT (Z2 + .5):Z3 = 0
525  IF Z$(Z1,Z2) = A$ THEN Z3 = 1
526  RETURN 
527 L2 = 2: IF Z5 >  = 5 THEN 529
528 L2 = 1
529 L3 = 2 * (Z4 - 1) + L2
530 I3 = 1:I0 = 1
531 G2$ = G1$(L3)
532  IF G5 = 1 THEN 541
533 L3 = Z5: IF Z5 <  = 4 THEN 535
534 L3 = Z5 - 4
535 G3$ = "IV"
536  IF L3 = 4 THEN 540
537  IF L3 = 3 THEN G3$ = "]I["
538  IF L3 = 2 THEN G3$ = "]["
539  IF L3 = 1 THEN G3$ = "I"
540 G2$ = G2$ + " " + G3$
541  RETURN 
542  TEXT : HOME : PRINT  CHR$ (4);"RUN HELLO"
543  PRINT "   1    2    3    4    5    6    7    8"
544  INVERSE 
545  PRINT "1      ANTARES      ";: NORMAL : PRINT "       SIRIUS"
546  PRINT "2       RIGEL       ";: INVERSE : PRINT "       DENEB       "
547  PRINT "3      PROCYON      ";: NORMAL : PRINT "       CAPELLA
548  PRINT "4       VEGA        ";: INVERSE : PRINT "     BETELGEUSE    "
549  PRINT "5      CANOPUS      ";: NORMAL : PRINT "      ALDEBARAN"
550  PRINT "6      ALTAIR       ";: INVERSE : PRINT "      REGULUS      "
551  PRINT "7    SAGITTARIUS    ";: NORMAL : PRINT "      ARCTURUS"
552  PRINT "8      POLLUX       ";: INVERSE : PRINT "       SPICA       "
553  GOTO 91
554  IF T = T0 THEN  PRINT "EFFICIENCY RATING NOT AVAILABLE YET.": GOTO 91
555  GOSUB 608: PRINT "YOUR EFFICIENCY RATING TO DATE IS: ";
556  PRINT (((K7 - K9) / (T - T0)) * 1000)
557  GOTO 91
558  POKE 34,16: POKE 35,24: POKE 32,1: POKE 33,17: HOME : INVERSE 
559  FOR I = 1 TO 8: GOSUB 438: IF I = 8 THEN 563
560  PRINT : NEXT 
561  INPUT "RATING WANTED:";ER
562  PRINT "NUMBER OF STARDATES=";: PRINT (1000 * K7) / ER: GOTO 91
563  POKE 33,40: POKE 32,0: POKE 34,1: POKE 35,15: VTAB 15: HTAB 1
564  NORMAL : RETURN 
565  POKE 34,16: POKE 35,24: POKE 32,18: POKE 33,11: HOME 
566  PRINT "STARDATE :"
567  PRINT "CONDITION:"
568  PRINT "QUADRANT :"
569  PRINT "SECTOR   :"
570  PRINT "ENERGY   :"
571  PRINT "PH TORPS :"
572  PRINT "SHIELDS  :"
573  POKE 32,0: POKE 33,40: POKE 34,0: POKE 35,15: HOME : RETURN 
574  POKE 33,9: POKE 32,30: POKE 34,16: POKE 35,24: HOME 
575  NORMAL 
576  PRINT  INT (T * 10) / 10
577  IF C$ = "RED" THEN  FLASH : PRINT C$: NORMAL : GOTO 580
578  IF C$ = "YELLOW" THEN  INVERSE : PRINT C$: NORMAL : GOTO 580
579  PRINT C$
580  PRINT Q1",";: INVERSE : PRINT Q2: NORMAL 
581  PRINT  INT (S1 + .5)",";: INVERSE : PRINT  INT (S2 + .5): NORMAL 
582  PRINT E + S
583  PRINT P
584  PRINT S
585  POKE 33,40: POKE 32,0: POKE 34,0: POKE 35,15: VTAB 1: RETURN 
586  IF D(2) < 0 THEN  RETURN 
587  HTAB Y3 * 2: VTAB X3 + 16: PRINT A$;: HTAB 1: VTAB 15: RETURN 
588  IF D(2) < 0 THEN  CALL 37216: RETURN 
589  FOR W = X3 - 1 TO X3 + 1
590  IF W < 1 OR W > 8 THEN 592
591 R$(W) = " " + Z$(W,Y3) + " "
592  NEXT W
593  HTAB Y3 * 2: VTAB X3 + 16: PRINT A$;: FOR W = 0 TO 300: NEXT 
594  FOR W = X3 - 1 TO X3 + 1
595  IF W < 1 OR W > 8 THEN 598
596  VTAB W + 16: HTAB Y3 * 2 - 1: IF Y3 = 1 THEN  HTAB Y3 * 2: PRINT  RIGHT$ (R$(W),2);: GOTO 598
597  PRINT R$(W);
598  NEXT W
599  CALL 37472
600  FOR W = 1 TO 100: NEXT : INVERSE 
601  IF A$ = "B" OR A$ = "K" THEN A$ = " ": CALL 37216
602  FOR W = X3 - 1 TO X3 + 1
603  IF W < 1 OR W > 8 THEN 606
604  VTAB W + 16: HTAB Y3 * 2 - 1: IF Y3 = 1 THEN  HTAB Y3 * 2: PRINT  RIGHT$ (R$(W),2);: GOTO 606
605  PRINT R$(W);
606  NEXT : HTAB Y3 * 2: VTAB X3 + 16: PRINT A$;: NORMAL : HTAB 1: VTAB 15: RETURN 
607  PRINT "BLOAD TREK ]I[-4": RETURN 
608  PRINT "YOU HAVE DESTROYED "(K7 - K9)" KLINGONS"
609  PRINT "IN "(T - T0)" STARDATES."
610  RETURN 